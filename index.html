<!DOCTYPE html>
<html style="height: 100%;">

<head>
  <!-- <meta name="viewport" content="width=device-width, initial-scale=1.0"> -->

  <!-- <title>webGLPBR</title> -->
</head>

<body style="margin: 0; height: 100%;">
  <canvas id="pbr-test" style="height: 100%; width: 100%;  background: black;"></canvas>
</body>
<script type="x-shader/x-vertex" id="vs">
  #version 300 es
  precision highp float;
  
  in vec4 a_POSITION;
  in vec3 a_Tangent;
  in vec3 a_NORMAL;
  in vec2 a_TEXCOORD_0;
  
  uniform mat4 u_projectionM;
  uniform mat4 u_modelM;
  uniform mat4 u_viewM;
  uniform mat4 u_normalM;
  uniform mat4 u_rotateM;
  
  out vec3 vPosition;
  out vec2 vTextureUv;
  out vec3 vNormal;
  out vec3 vTangent;
  
  void main(){
    gl_Position=u_projectionM*u_viewM*u_rotateM*a_POSITION;
    vNormal=mat3(u_modelM)*a_NORMAL;
    
    vec4 nor=u_rotateM*vec4(a_NORMAL,1.);
    vNormal=normalize(vec3(nor.x,nor.y,nor.z));
    nor=u_rotateM*vec4(a_Tangent,1.);
    vTangent=normalize(vec3(nor.x,nor.y,nor.z));
    
    vTextureUv=a_TEXCOORD_0;
    vPosition=gl_Position.xyz;
  }
</script>
<script type="x-shader/x-fragment" id="fs">
  #version 300 es
  precision highp float;
  
  in vec3 vPosition;
  in vec3 vNormal;
  in vec3 vTangent;
  in vec2 vTextureUv;
  
  uniform vec3 uLightPosition;
  uniform vec3 uLightColor;
  uniform float uLightRadius;
  uniform vec3 uCamPosition;
  
  uniform sampler2D uRadianceMap;
  uniform sampler2D uIrradianceMap;
  uniform sampler2D uBaseColorMap;
  uniform sampler2D uMetalRoughMap;
  uniform sampler2D uNormalMap;
  uniform sampler2D uEmissiveMap;
  uniform sampler2D uAOMAP;
  //uniform samplerCube 	depthMap;
  
  out vec4 outColor;
  
  #define saturate(x)clamp(x,0.,1.)
  #define PI 3.14159265359
  #define invPI 0.3183098861837697
  #define invTWO_PI 0.15915494309
  
  vec3 getNormap(vec3 t,vec3 n,vec3 color){
      vec3 b=cross(t,n);
      b=normalize(b);
      mat3 tbn=mat3(t,b,n);
      return normalize(tbn*color);
  }
  
  vec3 Diffuse_Lambert(vec3 diff){
      return diff*(1./PI);
  }
  
  float D_GGX(float roughness,float NoH){
      float m=roughness*roughness;
      float m2=m*m;
      float d=(NoH*m2-NoH)*NoH+1.;// 2 mad
      return m2/(PI*d*d);// 4 mul, 1 rcp
  }
  
  float Vis_Smith(float roughness,float NoV,float NoL){
      float a=roughness*roughness;
      float a2=a*a;
      
      float Vis_SmithV=NoV+sqrt(NoV*(NoV-NoV*a2)+a2);
      float Vis_SmithL=NoL+sqrt(NoL*(NoL-NoL*a2)+a2);
      return 1./(Vis_SmithV*Vis_SmithL);
  }
  
  vec3 F_Schlick(vec3 specularColor,float VoH){
      float Fc=pow(1.-VoH,5.);
      return Fc+(1.-Fc)*specularColor;
  }
  
  float getAttenuation(vec3 lightPosition,vec3 vertexPosition,float lightRadius){
      float r=lightRadius;
      vec3 L=lightPosition-vertexPosition;
      float dist=length(L);
      float d=max(dist-r,0.);
      L/=dist;
      float denom=d/r+1.;
      float attenuation=1./(denom*denom);
      float cutoff=.0052;
      attenuation=(attenuation-cutoff)/(1.-cutoff);
      attenuation=max(attenuation,0.);
      
      return attenuation;
  }

  vec3 getHdr(vec4 src){
    vec3 c = vec3(1., 1., 1.);
    float e = src.w * 255.0 - 128.0;
    e = pow(2.0, e);
    c.x = src.x * e;
    c.y = src.y * e;
    c.z = src.z * e;
    return c;
  }

  
  vec2 getuv(vec3 p){
    float theta = acos(p.y);
    float phi = atan(p.z, p.x);
    if (phi < 0.0) {
      phi += 2.0 * PI;
    }
    vec2 s;
    s.x = 1.0 - phi * invTWO_PI;
    s.y = theta * invPI;
    return s;
  }
  
  vec4 lerpMip(vec3 lookup, float mip){
    vec2 radianceUV = getuv(lookup);
    radianceUV.y *= 0.125;
    float mip1 = floor(mip);
    float mip2 = mip1 + 1.0;
    mip2 = clamp(mip2, 0., 5.);
    vec2 mip1uv = vec2(radianceUV.x, radianceUV.y + mip1 * 0.125);
    vec2 mip2uv = vec2(radianceUV.x, radianceUV.y + mip1 * 0.125);
    return vec4(mip1uv.x, mip1uv.y, mip2uv.x, mip2uv.y);
  }

  vec3 toneMap(vec3 src){
    vec3 color=src/(1.+src);
    color=pow(color,vec3(1./2.2,1./2.2,1./2.2));
    return color;
  }

  vec3 EnvBRDFApprox(vec3 specularColor, float roughness, float NoV){
    const vec4 c0 = vec4( -1.0, -0.0275, -0.572, 0.022 );
    const vec4 c1 = vec4( 1.0, 0.0425, 1.04, -0.04 );
    vec4 r = roughness * c0 + c1;
    float a004 = min( r.x * r.x, exp2( -9.28 * NoV ) ) * r.x + r.y;
    vec2 AB = vec2( -1.04, 1.04 ) * a004 + r.zw;
    AB.y *= saturate( 50.0 * specularColor.y );
    return specularColor * AB.x + AB.y;
  }
  
  void main(){
      vec3 albeoColor=texture(uBaseColorMap,vTextureUv).xyz;
      vec3 emmisColor=texture(uEmissiveMap,vTextureUv).xyz;
      vec3 ao=texture(uAOMAP,vTextureUv).xyz*1.5;
      
      vec3 mrColor=texture(uMetalRoughMap,vTextureUv).xyz;
      float metallic=mrColor.z;
      float roughness=mrColor.y;
      float specular=mrColor.y;
      
      vec3 diffuseColor=albeoColor-albeoColor*metallic;
      vec3 specularColor=mix(vec3(.08*specular),albeoColor,metallic);
      
      vec3 texN=texture(uNormalMap,vTextureUv).xyz;
      texN=texN*2.-1.;
      
      vec3 N=vNormal;
      vec3 V=normalize(uCamPosition-vPosition);
      vec3 L=normalize(uLightPosition-vPosition);
      vec3 H=normalize(V+L);
      
      float NoL=saturate(dot(N,L));
      float NoV=saturate(dot(N,V));
      float VoH=saturate(dot(V,H));
      float NoH=saturate(dot(N,H));
      
      float D=D_GGX(roughness,NoH);
      float Vis=Vis_Smith(roughness,NoV,NoL);
      vec3 F=F_Schlick(specularColor,VoH);
      
      vec3 d=Diffuse_Lambert(diffuseColor);
      vec3 s=D*Vis*F;
      vec3 color=uLightColor*(d+s)*NoL;
      float attenuation=getAttenuation(uLightPosition,vPosition,uLightRadius);
      
      color=color*attenuation*ao;
      color+=(emmisColor*1.5);

      float numMips = 5.0;
      float mip = clamp(log2(roughness) + numMips, 0.0, numMips);
      float mixmip = fract(mip);
      vec3 lookup = -reflect(V, N);

      vec4 mipuv = lerpMip(lookup, mip);
      vec3 radiance1 = getHdr(texture(uRadianceMap, mipuv.xy));
      vec3 radiance2 = getHdr(texture(uRadianceMap, mipuv.xy));
      vec3 radiance = mix(radiance1, radiance2, vec3(mixmip, mixmip, mixmip));
      vec3 irradiance = getHdr(texture(uIrradianceMap, getuv(vNormal)));

      vec3 reflectance = EnvBRDFApprox(specularColor,roughness, NoV);
      d = diffuseColor * irradiance;
      s = radiance * reflectance;
      color = color + s + d;
      //color = toneMap(color);

      outColor=vec4(color,1.);
  }
</script>
<script src="./index.js"></script>

</html>